#!/usr/bin/awk -f

BEGIN {
    isBuffered = 1
    ARGB = 1
    while (1) {
	if (ARGV[ARGB] ~ /^(--(help|man|usage)|-[Hh?])$/) {
	    print "Condense subsequent #'s at the end of a line or if a $ sigil is following (###$)"
	    print "into a count of those hashes."
	    print ""
	    print "Usage: countHashes [-u|--unbuffered] FILE [...] [-?|-- -h|-H|--man|--usage]"
	    print "Usage: cat FILE [...] | countHashes [-u|--unbuffered]"
	    isHelp = 1
	    exit
	} else if (ARGV[ARGB] == "--unbuffered" || ARGV[ARGB] == "-u") {
	    delete ARGV[ARGB++]
	    isBuffered = 0
	} else {
	    break
	}
    }
}
{
    while (match($0, /#+(\$|$)/)) {
	hashes = substr($0, RSTART, RLENGTH)
	if (substr(hashes, length(hashes)) == "$") {
	    hashes = substr(hashes, 1, length(hashes) - 1)
	}
	$0 = substr($0, 1, RSTART - 1) length(hashes) substr($0, RSTART + RLENGTH)
    }
    print
    if (! isBuffered) fflush()
}
