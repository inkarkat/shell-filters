#!/usr/bin/awk -f

BEGIN {
    if (ARGV[1] ~ /^(--(help|man|usage)|-[Hh?])$/) {
	print "Print the lowest and highest number (separated by a space / SEPARATOR) found in"
	print "ARGS or the input."
	print ""
	print "Usage: minmax [-v OFS=SEPARATOR] ARG [...] [-?|-H|--man|--usage]"
	print "Usage: cat FILE [...] | minmax [-v OFS=SEPARATOR]"
	isHelp = 1
	exit
    }
}

function tally(value)
{
    min = (value < min ? value : min)
    max = (value > max ? value : max)
}
BEGIN {
    min = 2^PREC
    max = -1 * min
    if (ARGC > 1) {
	for (i = 1; i < ARGC; ++i) {
	    tally(ARGV[i])
	}
	exit 0
    }
}
{
    for (i = 1; i <= NF; ++i) {
	tally($i)
    }
}
END {
    if (! isHelp) printf "%d%s%d\n", min, OFS, max
}
