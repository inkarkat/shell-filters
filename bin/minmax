#!/usr/bin/awk -f

BEGIN {
    separator = OFS
    ARGB = 1
    while (1) {
	if (ARGV[ARGB] ~ /^(--(help|man|usage)|-[Hh?])$/) {
	    print "Print the lowest and highest number (separated by a space / SEPARATOR) found in"
	    print "ARGS or the input."
	    print ""
	    print "Usage: minmax [-- -s|--separator SEPARATOR] ARG [...] [-?|-- -h|-H|--man|--usage]"
	    print "Usage: cat FILE [...] | minmax [-- -s|--separator SEPARATOR]"
	    isHelp = 1
	    exit
	} else if (ARGV[ARGB] == "--separator" || ARGV[ARGB] == "-s") {
	    delete ARGV[ARGB++]
	    separator = ARGV[ARGB]
	    delete ARGV[ARGB++]
	} else {
	    break
	}
    }
}

function tally(value)
{
    min = (value < min ? value : min)
    max = (value > max ? value : max)
}
BEGIN {
    min = 2^PREC
    max = -1 * min
    if (ARGC > ARGB) {
	for (i = ARGB; i < ARGC; ++i) {
	    tally(ARGV[i])
	}
	exit 0
    }
}
{
    for (i = 1; i <= NF; ++i) {
	tally($i)
    }
}
END {
    if (! isHelp) printf "%d%s%d\n", min, separator, max
}
