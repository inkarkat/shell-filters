#!/bin/sed -f

## Remove subsequent triples of lines that are identical, and join other triples in
## the form E1 != E2 == E3 (or E1 <- (two missing elements).
##
## Usage: filterIdenticalTriples FILE [...]
## Usage: cat FILE [...] | filterIdenticalTriples

${ s/$/ <- (two missing elements)/; b }
N
${
    s/\([^\n]*\)\n\1$/\1 == \1 <- (missing element)/
    t
    s/\([^\n]*\)\n\([^\n]*\)$/\1 != \2 <- (missing element)/
    b
}
N
/^\([^\n]*\)\n\1\n\1$/d
s/^\([^\n]*\)\n\1\n\([^\n]*\)$/\1 == \1 != \2/
s/^\([^\n]*\)\n\([^\n]*\)\n\2$/\1 != \2 == \2/
s/^\([^\n]*\)\n\([^\n]*\)\n\1$/\1 != \2 != \1 (== \1)/
s/^\([^\n]*\)\n\([^\n]*\)\n\([^\n]*\)$/\1 != \2 != \3/
t
