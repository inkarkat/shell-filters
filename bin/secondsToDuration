#!/bin/bash

printUsage()
{
    cat <<HELPTEXT
Treat any integer in the input as seconds and convert them into
[[[Ww ][Dd ] HH:]MM:]SS or 00:SS. (Unless the text around the integer (with the
integer itself represented by &) matches SKIP&PATTERN.)
HELPTEXT
    printf 'Usage: %q %s\n' "$(basename "$1")" 'SECONDS [...] [-?|-h|--help]'
    printf 'Usage: cat FILE [...] | %q %s\n' "$(basename "$1")" '[-s|--skip SKIP&PATTERN [...]]'
}
skipPattern=
while [ $# -ne 0 ]
do
    case "$1" in
	--help|-h|-\?)	shift; printUsage "$0"; exit 0;;
	--skip|-s)	shift; skipPattern="${skipPattern}${skipPattern:+|}${1:?}"; shift;;
	--)		shift; break;;
	-*)		{ echo "ERROR: Unknown option \"$1\"!"; echo; printShortUsage "$0"; } >&2; exit 2;;
	*)		break;;
    esac
done

awk -F $'\n' \
    -v "skipPattern=${skipPattern//\\/\\\\}" \
'
function convert(seconds,      hours, minutes)
{
    weeks = int(seconds / 604800)
    seconds = seconds - (604800 * weeks)
    days = int(seconds / 86400)
    seconds = seconds - (86400 * days)
    hours = int(seconds / 3600)
    seconds = seconds - (3600 * hours)
    minutes = int(seconds / 60)
    seconds = seconds - (60 * minutes)

    return \
	(weeks > 0 ? weeks "w " : "") \
	(days > 0 ? days "d " : "") \
	(weeks > 0 || days > 0 || hours > 0 ? sprintf("%02d", hours) ":" : "") \
	(weeks > 0 || days > 0 || hours > 0 || minutes > 0 ? sprintf("%02d", minutes) ":" : "") \
	(weeks > 0 || days > 0 || hours > 0 || minutes > 0 ? sprintf("%02d", seconds) : sprintf("00:%02d", seconds))
}
function shallowclone(target, source,      e)
{
    for (e in source) target[e] = source[e]
}
function join(n, list, seps,      i, result) {
    for (i = 1; i <= n; ++i) {
	result = result list[i] seps[i]
    }
    result = result list[++i]
    return result
}
BEGIN {
    if (ARGC > 1) {
	for (i = 1; i < ARGC; ++i) {
	    print convert(ARGV[i])
	}
	exit 0
    }
}
{
    n = split($0, rest, /\<[[:digit:]]+\>/, numbers)
    for (i = 1; i < n; ++i) {
	if (skipPattern != "") {
	    shallowclone(testNumbers, numbers)
	    testNumbers[i] = "&"
	    if (join(n, rest, testNumbers) ~ skipPattern) continue
	}

	numbers[i] = convert(numbers[i])
    }

    $0 = join(n, rest, numbers)
    print
}
' "$@"
