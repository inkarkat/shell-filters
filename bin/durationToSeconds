#!/bin/bash

: ${WORKDAY_SECONDS:=28800}; export WORKDAY_SECONDS
: ${WORKWEEK_SECONDS:=$((WORKDAY_SECONDS * 5))}; export WORKWEEK_SECONDS
: ${WORK_PREFIX:=w}; export WORK_PREFIX

printShortUsage()
{
    # Note: short followed by long option; if the user knows the short one, she can
    # skim the long one.
    printf 'Usage: %q %s\n' "$(basename "$1")" '"[Ww ][Dd |D-][HH:]MM:SS|[W[w]w ][D[w]d] [Hh] [Mm] [Ss]" [...] [-?|-h|--help]'
    printf 'Usage: cat FILE [...] | %q %s\n' "$(basename "$1")" '[-u|--unbuffered] [-s|--skip SKIP&PATTERN [...]] [-m|--match MATCH&PATTERN [...]]'
}
printUsage()
{
    # This is the short help when launched with no or incorrect arguments.
    # It is printed to stderr to avoid accidental processing.
    printShortUsage "$1" >&2
    printf >&2 '\nTry %q --help for more information.\n' "$(basename "$1")"
}
printLongUsage()
{
    # This is the long "man page" when launched with the help argument.
    # It is printed to stdout to allow paging with 'more'.
    cat <<HELPDESCRIPTION
Convert matches of [Ww ][Dd |D-][HH:]MM:SS into seconds.
HELPDESCRIPTION
    echo
    printShortUsage "$1"
    cat <<HELPTEXT
    --unbuffered|-u	    Immediately output each read line.
    --skip|-s SKIP&PATTERN  Do not convert if the text around the duration (with
			    the duration itself represented by &) matches
			    SKIP&PATTERN.
    --match|-m MATCH&PATTERN
			    Only convert if the text around the duration (with
			    the duration itself represented by &) matches
			    MATCH&PATTERN. Other occurrences (and those that
			    also match --skip SKIP&PATTERN) are not converted.
PATTERN is an AWK-style extended regular expression.
HELPTEXT
}

isBuffered=t
skipPattern=
matchPattern=
while [ $# -ne 0 ]
do
    case "$1" in
	--help|-h|-\?)	shift; printLongUsage "$0"; exit 0;;
	--unbuffered|-u)
			shift; isBuffered=;;
	--skip|-s)	shift; skipPattern+="${skipPattern:+|}${1:?}"; shift;;
	--match|-m)	shift; matchPattern+="${matchPattern:+|}${1:?}"; shift;;
	--)		shift; break;;
	-*)		{ echo "ERROR: Unknown option \"$1\"!"; echo; printUsage "$0"; } >&2; exit 2;;
	*)		break;;
    esac
done

awk -F $'\n' \
    -v "isBuffered=$isBuffered" \
    -v "skipPattern=${skipPattern//\\/\\\\}" \
    -v "matchPattern=${matchPattern//\\/\\\\}" \
'
function convert(duration,      calendar, n, c, cc, clock, result, hasMatch)
{
    n = split(gensub(/^.* /, "", 1, duration), clock, /:/)
    c = split(duration, calendar, / /)
    if (match(clock[1], /^[[:digit:]]+-/)) {
	calendar[c] = substr(clock[1], RSTART, RLENGTH - 1) "d"
	clock[1] = substr(clock[1], RSTART + RLENGTH)
    }

    for (cc = 1; cc <= c; ++cc) {
	if (calendar[cc] ~ "^[[:digit:]]+" ENVIRON["WORK_PREFIX"] "w$") {
	    result += substr(calendar[cc], 1, length(calendar[cc]) - 1) * ENVIRON["WORKWEEK_SECONDS"]
	    hasMatch = 1
	    continue
	}
	if (calendar[cc] ~ /^[[:digit:]]+w$/) {
	    result += substr(calendar[cc], 1, length(calendar[cc]) - 1) * 604800
	    hasMatch = 1
	    continue
	}
	if (calendar[cc] ~ /^[[:digit:]]+d$/) {
	    result += substr(calendar[cc], 1, length(calendar[cc]) - 1) * 86400
	    hasMatch = 1
	    continue
	}
	if (calendar[cc] ~ "^[[:digit:]]+" ENVIRON["WORK_PREFIX"] "d$") {
	    result += substr(calendar[cc], 1, length(calendar[cc]) - 1) * ENVIRON["WORKDAY_SECONDS"]
	    hasMatch = 1
	    continue
	}
	if (calendar[cc] ~ /^[[:digit:]]+h$/) {
	    result += substr(calendar[cc], 1, length(calendar[cc]) - 1) * 3600
	    hasMatch = 1
	    continue
	}
	if (calendar[cc] ~ /^[[:digit:]]+m$/) {
	    result += substr(calendar[cc], 1, length(calendar[cc]) - 1) * 60
	    hasMatch = 1
	    continue
	}
	if (calendar[cc] ~ /^[[:digit:]]+s$/) {
	    result += substr(calendar[cc], 1, length(calendar[cc]) - 1)
	    hasMatch = 1
	    continue
	}
    }

    if (n == 2) {
	result += clock[n - 1] * 60 + clock[n]
	hasMatch = 1
    } else if (n == 3) {
	result += clock[n - 2] * 3600 + clock[n - 1] * 60 + clock[n]
	hasMatch = 1
    }

    return (hasMatch ? result : duration)
}
function shallowclone(target, source,      e)
{
    for (e in source) target[e] = source[e]
}
function join(n, list, seps,      i, result) {
    for (i = 1; i <= n; ++i) {
	result = result list[i] seps[i]
    }
    result = result list[++i]
    return result
}
BEGIN {
    DURATION_PATTERN = "\\<([[:digit:]]+(" ENVIRON["WORK_PREFIX"] ")?w )?(([[:digit:]]+((" ENVIRON["WORK_PREFIX"] ")?d |-))?[[:digit:]]+(:[[:digit:]]{2}){1,2}|([[:digit:]]+(" ENVIRON["WORK_PREFIX"] ")?d )?([[:digit:]]+h )?([[:digit:]]+m )?([[:digit:]]+s)|([[:digit:]]+(" ENVIRON["WORK_PREFIX"] ")?d )?([[:digit:]]+h )?([[:digit:]]+m)|([[:digit:]]+(" ENVIRON["WORK_PREFIX"] ")?d )?([[:digit:]]+h)|([[:digit:]]+(" ENVIRON["WORK_PREFIX"] ")?d))\\>|\\<([[:digit:]]+(" ENVIRON["WORK_PREFIX"] ")?w)\\>"

    if (ARGC > 1) {
	for (i = 1; i < ARGC; ++i) {
	    if (ARGV[i] ~ "^" DURATION_PATTERN "$")
		print convert(ARGV[i])
	    else
		status = 1
	}
	exit status
    }
}
{
    n = split($0, rest, DURATION_PATTERN, durations)

    for (i = 1; i < n; ++i) {
	if (skipPattern != "" || matchPattern != "") {
	    shallowclone(testDurations, durations)
	    testDurations[i] = "&"
	    testJoin = join(n, rest, testDurations)
	    if (skipPattern != "" && testJoin ~ skipPattern) continue
	    if (matchPattern != "" && testJoin !~ matchPattern) continue
	}

	durations[i] = convert(durations[i])
    }

    $0 = join(n, rest, durations)
    print
    if (! isBuffered) fflush()
}
' "$@"
